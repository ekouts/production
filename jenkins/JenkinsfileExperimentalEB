#!/usr/bin/env groovy

def methods
def machinesList
/* 
 Initialization: checkout the production repository to load the files:
 - "util.groovy" with the functions used in the pipeline scripts
 - "Machines.groovy" with the list and properties of HPC systems
*/
stage('Initialization') {
    node('master') {
        checkout scm
        methods = load("$WORKSPACE/jenkins/util.groovy")
        machinesList = load("$WORKSPACE/jenkins/Machines.groovy")
    }
}

/* 
 Machine Selection: 
 - filters the user defined parameter "machines", accessible though "params.machines"
 - defines the runtime HPC systems of the project "machinesToRun" 
 - defines the configuration of the runtime HPC systems "machinesConfiguration"
*/
def machineConfigurations = []
stage('Machine Selection') {
    node('master') {
        def machinesToRun = machinesList.findAll({methods.machineCheck(params.machines, it.name)})
        if (!machinesToRun) {
            println "No machines were specified. Aborting...."
            currentBuild.result = "FAILURE"
            return
        }
        for (system in machinesToRun) {
            machineConfigurations.add(
                [machine:system, architectures: methods.getMachineConfiguration(
                        params.machines, system.name, system.archs as String[])])
        }
    }
}

/* 
 Build Stage: configures parallel builds for the different "machinesToRun"
 - starts a loop over all systems in "machineConfigurations"
  - if the machine has multiple architectures, starts a sub-loop over architectures
  - the user parameter "eb_prefix" is the environment variable EASYBUILD_PREFIX
  - EBVERSIONEASYBUILD stores the version of EasyBuild ("params.eb_version")
  - the target CDT version is selected by the user and stored in "params.cdt_version"
*/
def builds = [:]
stage('Build Stage') {
    for (system in machineConfigurations) {
        def machine = system.machine
        def architectures = system.architectures
        def machineName = machine.name
        for (architecture in architectures) {
            def machineLabel = architecture == "" ?
               machineName : 
               "$machineName-$architecture"
            println("Building $machineLabel: machine name is $machineName, architecture is $architecture")

            builds[machineLabel] = {
                println("Building $machineLabel: machine name is $machineName, architecture is $architecture")

                node(machineName) {
                    println("Building $machineLabel: machine name is $machineName, architecture is $architecture")
                    checkout scm
                    if (machine.unusePath != '') {
                        def unusePath = architecture == '' ?  
                            "$machine.unusePath" : 
                            "$machine.unusePath".replace('ARCH', architecture)
                        println("The unuse path is: $unusePath")
                    }
                    else {
                        println("The unuse path is empty")
                    }
                    /* Remove "--installpath" option and Matlab */
                    sh "sed -e '/MATLAB/d' -e 's/--installpath=.*//' $WORKSPACE/jenkins-builds/$machineLabel > buildlist.txt"

                    def loadeasybuild = architecture == "" ?
                        "module load EasyBuild-custom/cscs" :
                        "module load daint-$architecture EasyBuild-custom/cscs"

                    def switcheasybuild = 
                        "module switch EasyBuild EasyBuild/$eb_version"

                    def command = architecture == "" ? 
                        "srun -u -J $env.JOB_BASE_NAME -t 00:01:00 eb --show-config" :  
                        "srun -u -C $architecture -J $env.JOB_BASE_NAME -t 00:01:00 eb CrayGNU-19.10.eb CrayIntel-19.10.eb CrayPGI-19.10.eb --hidden --try-software-version=$params.cdt_version"
                    sh("""#!/bin/bash -l
                        echo -e "EasyBuild environment variables:"
                        echo 'export EASYBUILD_PREFIX=$params.eb_prefix'
                        echo 'export EASYBUILD_TMPDIR=\$EASYBUILD_PREFIX/tmp'
                        echo 'export EASYBUILD_SOURCEPATH=\$EASYBUILD_PREFIX/sources'
                        echo 'export EASYBUILD_BUILDPATH=/tmp/$env.BUILD_TAG'
                        export EASYBUILD_PREFIX="$params.eb_prefix"
                        export EASYBUILD_TMPDIR="\$EASYBUILD_PREFIX/tmp"
                        export EASYBUILD_SOURCEPATH="\$EASYBUILD_PREFIX/sources"
                        export EASYBUILD_BUILDPATH="/tmp/$env.BUILD_TAG"

                        $loadeasybuild
                        $switcheasybuild
                        eb --version
                        eb --show-config

                        echo -e "Running command: \n$command"
                        status=0
                        $command

                        status=\$[status+\$?]
                        exit \$status""")
                }
            }
        }
    }

    try {
        parallel builds
        currentBuild.result = "SUCCESS"
    } catch(err) {
        if (err.toString().contains('exit code 143')) {
            currentBuild.result = "ABORTED"
            println "The Build step was cancelled. Aborting..."
        }
        else if (err.toString().contains('Queue task was cancelled')) {
            currentBuild.result = "ABORTED"
            println "The Queue task was cancelled. Aborting..."
        }
        else {
            currentBuild.result = "FAILURE"
            println "The Build step failed. Exiting..."
        }
    }
}
